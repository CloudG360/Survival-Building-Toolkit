plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false
}

String targetVersion = rootProject.properties["mod_version"] == null ||
		rootProject.properties["mod_version"].toString().length() == 0
		   ? "-indev"
		   :"-v" + rootProject.properties["mod_version"].toString()


architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

		mappings loom.layered() {
			officialMojangMappings()

			if(project.parchment_version != "")
				parchment("org.parchmentmc.data:${project.parchment_version}@zip")
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	archivesBaseName = rootProject.archives_base_name
	version = project.properties["minecraft_version"] + targetVersion
	group = rootProject.maven_group

	repositories {
		maven {
			name = 'ParchmentMC'
			url = "https://maven.parchmentmc.org/"
		}

		// Mod Deps
		maven { url "https://maven.shedaniel.me/" }
		maven { url "https://maven.terraformersmc.com/releases/" }
		maven {
			name = "Modrinth"
			url = "https://api.modrinth.com/maven"
			content {
				includeGroup "maven.modrinth"
			}
		}
	}


	tasks.withType(JavaCompile).configureEach {
		options.encoding = "UTF-8"
		options.release.set(17)

		// Shows details related to the xlint "unchecked" and "derecation" warning notes.
		if (rootProject.properties["show_dev_warnings"] == "true") {
			it.options.deprecation = true
			it.options.compilerArgs << '-Xlint:unchecked'
		}
	}

	processResources {
		inputs.property "version", project.version
		filteringCharset "UTF-8"

		HashMap modifiedSrc = new HashMap<>(project.properties)
		modifiedSrc.put("mod_id", project.properties["archives_base_name"].toString().toLowerCase())

		filesMatching("fabric.mod.json") {
			expand modifiedSrc
		}

		filesMatching("META-INF/mods.toml") {
			expand modifiedSrc
		}
	}

	java {
		withSourcesJar()
	}

	jar {
		from("LICENSE.md") {
			rename { "${it}_${project.archivesBaseName}"}
		}
	}
}

tasks.register('updateDocTemplates', Copy) {
	group = "mod-utils"
	description = "Takes documents from a folder, expands any template variables, and then copies them to the root."
	doNotTrackState("Stops gradle from screaming - It doesn't let files get copied to the root if tracking state.")

	HashMap modifiedSrc = new HashMap<>(project.properties)

	from "/template_docs"

	into "/"
	include "**"
	exclude "media/**"

	expand modifiedSrc
}
